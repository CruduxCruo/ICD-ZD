ACTOR BaseWeapon : Weapon
{
	Weapon.BobStyle		Smooth
	Weapon.BobSpeed		0.75
	Weapon.BobRangeX		0.2
	Weapon.BobRangeY		0.4
	Scale				0.9
}

// Puffs

ACTOR ICD_Puff_Spawn_2
{
	Game Doom
	+NOBLOCKMAP
	+NOGRAVITY
	+ALLOWPARTICLES
	+RANDOMIZE
	RenderStyle Translucent
	Alpha 0.8
	scale 0.4
	States
	{
		Spawn:
			SPUF ABCD 1 BRIGHT Light("ICD_Sparks")
			stop
	}
}

// Sparks (I hate DDF)

ACTOR ICD_Gun3_Sparks
{
	Height 8
	Radius 8
	Speed 32
	Mass 2
	+MISSILE
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	renderstyle add
	States
	{
		Spawn:
			TNT1 A 0
			FRAG AAA 2 BRIGHT
			FRAG AAAA 2 BRIGHT A_FadeOut
		 Stop
	}
}

ACTOR ICD_Flare_Sparks_Yellow
{
	Height 8
	Radius 8
	Speed 32
	Mass 2
	+MISSILE
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	renderstyle add
	States
	{
	Spawn:
			TNT1 A 0
			FRAG AAAAAA 2 BRIGHT
			FRAG AAAA 2 BRIGHT A_FadeOut
			Stop
	}
}

ACTOR ICD_Flare_Sparks_Red
{
	Height 8
	Radius 8
	Speed 32
	Mass 2
	+MISSILE
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	renderstyle add
	States
	{
		Spawn:
			TNT1 A 0
			FRAG FFFFFF 2 BRIGHT
			FRAG FFFF 2 BRIGHT A_FadeOut
			Stop
	}
}



//Smoke

ACTOR ICD_GunSmokeBase
{
	Height 8
	Radius 8
	Speed 6
	Mass 2
	Scale 0.4
	+MISSILE
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	+NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	renderstyle translucent
	Alpha 0.13
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_ScaleVelocity(2.5)
			TNT1 A 2
			TNT1 A 0 A_ScaleVelocity(0.5)
			TNT1 A 0 A_ScaleVelocity(frandom(0.5,1))
			PUF2 ABCDEFGHIJKLKMNOPQRSTUVWXYZ 1
			PUF3 ABC 1 A_FadeOut(0.043)
			Stop
	}
}

ACTOR ICD_SSGSmoke1 : ICD_GunSmokeBase { }

ACTOR ICD_PistolSmoke1 : ICD_GunSmokeBase
{
	Speed 3
	scale 0.25
}


ACTOR ICD_MiniSmoke : ICD_GunSmokeBase
{
	Speed 5
	Scale 0.4
}

ACTOR ICD_RPUFF : ICD_GunSmokeBase
{
	Scale 0.5
	Alpha 0.5
	States
	{
		Spawn:
			TNT1 A 0
			SMOK A 5
			SMOK B 5 A_FadeOut
			SMOK C 5 A_FadeOut
			SMOK D 5 A_FadeOut
			SMOK E 5
			stop
	}
}

ACTOR ICD_FlareSmoke : ICD_GunSmokeBase
{
	Speed 0.5
	Scale 0.13
	Alpha 0.4
	States
	{
		Spawn:
			RLSR C 6 BRIGHT
			SMOK A 1 ThrustThingZ(0, 1.2, 0, 1)
			TNT1 A 0 A_Jump(175, "SpawnJump")
			TNT1 A 0 A_PlaySound("weapons/flare/flaresoftignite")
			TNT1 A 0 A_SpawnItemEx("ICD_Flare_Sparks_Red",0,0,0,frandom(-2,2), frandom(-2,2), frandom(2,3),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SpawnItemEx("ICD_Flare_Sparks_Yellow",0,0,0,frandom(-2,2), frandom(-2,2), frandom(2,3),0,SXF_NOCHECKPOSITION)
			SMOK A 2
			SMOK A 0 // Flare2
		SpawnJump:
			SMOK A 2
			SMOK B 4 A_FadeOut
			SMOK C 4 A_FadeOut
			SMOK D 4 A_FadeOut
			SMOK E 4 A_FadeOut
			Stop
	}
}

ACTOR ICD_FlareSmoke2 : ICD_FlareSmoke
{
	Scale 0.6
}

// GSA Code! Yay!

ACTOR ICD_GS_CasingTemplate
{
	Height 2
	Radius 2
	Speed 3
	Mass 0
	BounceType "None"
	BounceFactor 0.5
	WallBounceFactor 0.5
	BounceCount 4
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+MOVEWITHSECTOR
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+NOTELEPORT
	+FORCEXYBILLBOARD
}

ACTOR ICD_GS_PistolCasing : ICD_GS_CasingTemplate
{
	Scale 0.35
	BounceSound "weapons/effects/smallcasing"
	States
	{
		Spawn:
			CAS2 A 0 A_Playsound("NULL")
			TNT1 A 0 A_ScaleVelocity (frandom(2,3))
			CAS2 ABCDEF 2 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Goto Spawn+2
		Death:
			TNT1 A 0 A_Jump(256,"Lie1", "Lie2")
			Loop
		Lie1:
			CAS2 A 2500
			Stop
		Lie2:
			CAS2 F 2500
			Stop
	}
}

ACTOR ICD_GS_HeavyCal : ICD_GS_CasingTemplate
{
	Scale 0.3
	BounceSound "weapons/effects/largecasing"
	States
	{
		Spawn:
			CAS7 A 0 A_Playsound("NULL")
			TNT1 A 0 A_ScaleVelocity (frandom(2,3))
			CAS7 A 0 A_Jump(256, "Spin1")
			Goto Spawn+2
		Spin1:
			TNT1 A 0 A_Jump(128,4)
			CAS7 FDCBEAF 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Death:
			CAS7 B 2500
			Stop

	}
}

ACTOR ICD_GS_MinigunSpacer : ICD_GS_CasingTemplate
{
	Scale 0.31
	BounceSound "NULL"
	States
	{
		Spawn:
			TNT1 A 0 A_Playsound("NULL")
			TNT1 A 0 A_ScaleVelocity (frandom(2,3))
			TNT1 A 0 A_Jump(256, "Spin1")
			Goto Spawn+2
		Spin1:
			TNT1 A 0 A_Jump(128,3)
			SPCR ABCD 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Death:
			SPCR B 2500
			Stop

	}
}

ACTOR ICD_GS_EmptyShell : ICD_GS_CasingTemplate
{
	Scale 0.78
	BounceSound "weapons/shellbounce"
	States
	{
		Spawn:
			TNT1 A 0
			CAS3 A 0
			TNT1 A 0 A_ScaleVelocity (frandom(2,3))
			CAS3 A 0 A_Jump(256, "Spin1", "Spin2", "Spin3", "Spin4", "Spin5")
			Goto Spawn+1
		Spin1:
			CAS3 ABCD 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Spin2:
			CAS3 EFCD 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Spin3:
			CAS3 DCBA 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Spin4:
			CAS3 BCDE 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Spin5:
			CAS3 CDEF 3 A_CustomMissile("ICD_GS_EmptyCasingSmoke",0,0,0,2,0)
			Loop
		Death:
			CAS3 A 0 A_Jump(256,"Lie1","Lie2")
		 	Loop
		Lie1:
			CAS3 C 2500
			Stop
		Lie2:
			CAS3 A 2500
			stop
	}
}

ACTOR ICD_GS_EmptyCasingSmoke
{
	Renderstyle Translucent
	Alpha 0.10
	Scale 0.35
	+NOINTERACTION
	States
	{
		Spawn:
			SMKE ABCDEFGH 2
			stop
	}
}

ACTOR ICD_FlakSmoke : ICD_GS_EmptyCasingSmoke
{
	scale 0.7
}

ACTOR ICD_ShellSpawner
{
	Radius 1
	Height 1
	Speed 15
	PROJECTILE
	States
	{
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0
		Stop
	}
}

ACTOR ICD_ShotgunShellSpawner1 : ICD_ShellSpawner
{
	 States
	 {
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0 A_CustomMissile("ICD_GS_EmptyShell",2,0,90+random(-15,15),2,55+random(-10,10))
			Stop
	 }
}

ACTOR ICD_PistolCasingSpawner : ICD_ShellSpawner
{
	 States
	 {
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0 A_CustomMissile("ICD_GS_PistolCasing",2,0,270+random(-15,15),2,55+random(-10,10))
			Stop
	 }
}

ACTOR ICD_MinigunCasingSpawner : ICD_ShellSpawner
{
	States
	{
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0 A_CustomMissile("ICD_GS_HeavyCal",2,0,270+random(-15,15),2,55+random(-10,10))
			Stop
	}
}

ACTOR ICD_MinigunSpacerSpawner : ICD_ShellSpawner
{
	States
	{
		Spawn:
			TNT1 A 1
		Death:
			TNT1 A 0 A_CustomMissile("ICD_GS_MinigunSpacer",2,0,270+random(-15,15),2,55+random(-10,10))
			Stop
	}
}

ACTOR ICD_BulletPuff : BulletPuff
{
	VSpeed 0.5
	+NOEXTREMEDEATH
	Renderstyle Translucent
	Alpha 1
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("world/effect/bullimpact", 6)
			PUFF AB 2 BRIGHT
			TNT1 AAAA 0 A_SpawnItemEx("ICD_BulletPuffSpark", 0,0,0,random(-5,5),random(-5,5),random(-5,5),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_Jump(179, 3)
			TNT1 A 0 A_Jump(179, 2)
			TNT1 A 0 A_PlaySound("world/effect/richochet", 5)
			TNT1 A 0 A_FadeOut(0.8,0)
			SMKE BCDEFGH 2
			Stop
	}
}

ACTOR ICD_ShotgunBulletPuff : BulletPuff
{
	VSpeed 0.5
	+NOEXTREMEDEATH
	Renderstyle Translucent
	Alpha 0.2
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("world/effect/bullimpact", 6)
			TNT1 AAAA 0 A_SpawnItemEx("ICD_BulletPuffDust", 0,0,0,random(-5,5),random(-5,5),random(-5,5),0,SXF_NOCHECKPOSITION,75)
			SMKE ABCDEFGH 2
			Stop
	}
}

ACTOR ICD_BulletPuffSpark
{
	Height 8
	Radius 8
	Speed 32
	Mass 2
	+MISSILE
	+MOVEWITHSECTOR
	+THRUACTORS
	+NOTELEPORT
	+NODAMAGETHRUST
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.4
	//renderstyle add
	States
	{
		Spawn:
			TNT1 A 0
			FRAG A 1 BRIGHT
			TNT1 A 0 A_Jump(179, 2)
			TNT1 A 0 A_Explode(1, 32, 1, 0, 1)
			FRAG A 1 BRIGHT
			FRAG AAAA 2 BRIGHT A_FadeOut
			Stop
	}
}

ACTOR ICD_BulletPuffDust
{
	Height 8
	Radius 8
	Speed 10
	Mass 2
	BounceType "None"
	BounceFactor 0.5
	WallBounceFactor 0.5
	BounceCount 4
	+MISSILE
	-NOGRAVITY
	+THRUACTORS
	+MOVEWITHSECTOR
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	+NOTELEPORT
	+FORCEXYBILLBOARD
	renderstyle translucent
	Alpha 0.75
	States
	{
		Spawn:
			TNT1 A 0
			FRAG B 1
			Loop
		Death:
			FRAG B 350
			FRAG BBBB 105 A_FadeOut
			Stop
	}
}