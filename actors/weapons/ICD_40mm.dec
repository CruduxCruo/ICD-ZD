//Grenade Launcher
ACTOR ICD_40mm : BaseWeapon
{
	Game Doom
	Tag "$TAG_40mm"
	Weapon.AmmoType1 "ICD_40mmMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 0
	Weapon.AmmoType2 "RocketAmmo"
	Weapon.UpSound "weapons/generic/weaponup"
	Inventory.PickupMessage "$Got40mm"
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Select:
			GREN A 1 A_Raise
			Loop
		Deselect:
			GREN A 1 A_Lower
			Loop
		Ready:
			GREN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			GREN A 0 A_JumpIfInventory("ICD_40mmMagazine",1,2)
			GREN A 0 A_JumpIfInventory("RocketAmmo",1,"Reload")
			Goto Ready
			GREN A 2 A_GunFlash
			GREN A 0 A_PlaySound("weapon/40mm/fire",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN A 2 A_FireCustomMissile("ICD_40mmGrenade",0,1,-5,0,0,4)
			GREN A 0 A_AlertMonsters
			GREN A 2
			GREN A 1
			GREN D 3
			GREN A 1
			GREN B 3 A_FireCustomMissile("ICD_40mm_Shell_Spawn", 0, 0, 5, -4)
			GREN C 3
			GREN A 5 A_Refire
			Goto Ready
		Reload:
			GREN A 5 A_PlaySound("weapon/generic/emptyclick2",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN GH 4
			GREL A 8 A_PlaySound("weapon/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL J 2 A_PlaySound("weapon/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL AB 2
			GREL C 3
			GREL A 4
			GREL J 2 A_PlaySound("weapon/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL A 2
			GREL DE 3
			GREL A 4
			GREL J 2 A_PlaySound("weapon/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL A 2
			GREL FG 3
			GREL A 4
			GREL J 2 A_PlaySound("weapon/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL A 2
			GREL HI 3
			GREL A 4
			GREL J 2 A_PlaySound("weapon/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL A 10
			GREN H 3 A_PlaySound("weapon/40mm/close",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN G 3
			GREN A 2
			Goto Ready
		Altfire:
			GREN A 1
			GREN GH 3
			GREL A 6 A_PlaySound("weapon/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL J 2 A_PlaySound("weapon/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL A 2
			GREL HIA 3
			GREN H 3 A_PlaySound("weapon/40mm/close",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN G 3
			GREN A 4
			GREN A 2 A_FireCustomMissile("ICD_40mmSmokenade")
			GREN A 0 A_AlertMonsters
			GREN A 0 A_GunFlash
			GREN A 0 A_PlaySound("weapon/40mm/fire",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN A 2 ACS_NamedExecute("ICD_Recoil",0,6,8,300)
			GREN A 2
			GREN D 3
			GREN A 1
			GREN BC 3
			GREN A 5 A_Refire
			Goto Ready
		Flash:
		Altflash:
			GRNF A 2 A_Light1
			GRNF B 2 A_Light2
			GRNF C 2 A_Light1
			MISF B 0 A_Light0
			Stop

	}
}

ACTOR ICD_40mmMagazine : Ammo
{
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 5
	Inventory.Icon SHOTA0
	+INVENTORY.IGNORESKILL
}

ACTOR ICD_40mmGrenade
{
	Radius 8
	Height 8
	Speed 30
	Damage 20
	ReactionTime 75
	BounceType "Doom"
	BounceSound "weapon/40mm/bounce"
	Scale 0.8
	PROJECTILE
	-NOGRAVITY
	BounceType Doom
	States
	{
		Spawn:
			GRNL A 1 A_SpawnItemEx("ICD_RPUFF")
			Loop
		Death:
			BFE1 G 2
			BFE1 H 2 A_Explode(150,64)
			BFE1 I 0 A_SetAngle(angle+88)
			BFE1 I 0 //A_SpawnItemEx("ICD_40mm_Mushroom",0,0,0)
			BFE1 I 0 A_SetAngle(angle+88)
			BFE1 I 0 //A_SpawnItemEx("ICD_40mm_Mushroom",0,0,0)
			BFE1 I 0 A_SetAngle(angle+88)
			BFE1 I 0 //A_SpawnItemEx("ICD_40mm_Mushroom",0,0,0)
			BFE1 I 0 A_SetAngle(angle+88)
			BFE1 I 0 //A_SpawnItemEx("ICD_40mm_Mushroom",0,0,0)
			BFE1 IJKLM 2
			BFE1 N 2 //A_SpawnItemEx("ICD_Explosion_Smoke")
			BFE1 OPQRSTUVW 2
			Stop

	}
}