ACTOR ICD_Demon replaces Demon
{
	Health 160
	Radius 30
	Height 56
	Speed 11
	Mass 400
	Painchance 180
	Scale 1.1
	Monster
	BloodType "ICD_Blood"
	+FLOORCLIP
	SeeSound "demon/sight"
	AttackSound "demon/melee"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "$OB_DEMONHIT" // "%o was bit by a demon."
	States
	{
		Spawn:
			SARG A 5 A_Look
			Loop
		See:
			SARG AABBCCDD 2 A_Chase
			Loop
		Melee:
			SARG E 6 A_FaceTarget
			SARG E 7 A_FaceTarget
			SARG G 7 A_CustomMeleeAttack((6*random(1,10)), NULL, NULL, NULL, 1)
			Goto See
		Pain:
			SARG H 2
			SARG H 2 A_Pain
			Goto See
		Death:
			SARG I 8
			SARG J 8 A_Scream
			SARG K 4
			SARG L 4 A_NoBlocking
			SARG M 4
			SARG N -1
			Stop
		Raise:
			SARG N 5
			SARG MLKJI 5
			Goto See
	}
}

ACTOR ICD_Spectre : ICD_Demon replaces Spectre
{
	+SHADOW
	RenderStyle OptFuzzy
	BloodType "ICD_SpectreBlood"
	Alpha 0.5
	HitObituary "%o should've looked harder for that spectre"
	States
	{
		Melee:
			SARG E 7 A_FaceTarget
			SARG E 6 A_FaceTarget
			SARG G 6 A_CustomMeleeAttack((6*random(1,10)), NULL, NULL, NULL, 1)
			Goto See
	}
}
