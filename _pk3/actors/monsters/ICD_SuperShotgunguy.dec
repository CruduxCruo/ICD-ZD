// Even harder hitting motherf-cker.
// implemented by wildweasel

//RANGE_ATTACK=FORMER_HUMAN_SSG;
//SPARE_ATTACK=ENEMY_SHELL;

ACTOR ICD_SuperShotgunGuy
{
	Health 50
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	SeeSound "shotguy/sight"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "$OB_SHOTGUY"
	DropItem "ICD_Doublebarrel"
	States
	{
	Spawn:
		SPOS AA 5 A_Look
		Loop
	See:
		SPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		SPOS E 0 A_PlaySound("shotguy/sight") //PLAYSOUND("POSIT?")
		SPOS E 10 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/doublebarrel/fire", CHAN_WEAPON)
		SPOS F 1 BRIGHT A_CustomBulletAttack(9,9,8,(4/2*random(1,3)),"BulletPuff",360) //RANGE_ATTACK,
		TNT1 AAAAA 0 A_CustomMissile("ICD_Gun3_Sparks", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))//SPARE_ATTACK(ENEMY_GUN_SPARKS),
		SPOS F 3 BRIGHT 
		TNT1 AAAA 0 A_CustomMissile("ICD_SSGSmoke1", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))//SPARE_ATTACK(ENEMY_SMOKE),
		SPOS E 2 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/doublebarrel/fire", CHAN_WEAPON)
		SPOS F 1 BRIGHT A_CustomBulletAttack(9,9,8,(4/2*random(1,3)),"BulletPuff",360) //RANGE_ATTACK,
		TNT1 AAAAA 0 A_CustomMissile("ICD_Gun3_Sparks", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))//SPARE_ATTACK(ENEMY_GUN_SPARKS),
		SPOS F 3 BRIGHT 
		TNT1 AAAA 0 A_CustomMissile("ICD_SSGSmoke1", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))//SPARE_ATTACK(ENEMY_SMOKE),
		SPOS E 16 
		SPOS B 5 A_PlaySound("weapons/doublebarrel/open", CHAN_WEAPON)//PLAYSOUND(DBOPN),
		SPOS B 1 A_CustomMissile("ICD_ShotgunShellSpawner1", 32, 0, frandom(-35,-25))//SPARE_ATTACK,
		SPOS E 6 A_CustomMissile("ICD_ShotgunShellSpawner1", 32, 0, frandom(-35,-25))//SPARE_ATTACK,
		SPOS E 4 A_PlaySound("weapons/doublebarrel/load", CHAN_WEAPON)//PLAYSOUND(DBLOAD),
		SPOS B 3 
		SPOS A 4 A_PlaySound("weapons/doublebarrel/close", CHAN_WEAPON)//PLAYSOUND(DBCLS),
		Goto See
	Pain:
		SPOS G 3
		SPOS G 3 A_Pain
		Goto See
	Death:
		SPOS H 5
		SPOS I 5 A_Scream
		SPOS J 5 A_NoBlocking
		SPOS K 5
		SPOS L 500
		SPOS L 3 A_SpawnItem("ICD_Flies")
		SPOS L -1 A_SpawnItem("ICD_Flies")
		Stop
	XDeath:
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 5 A_NoBlocking
		SPOS PQRST 5
		SPOS U 400
		SPOS U 3 A_SpawnItem("ICD_Flies")
		SPOS U -1 A_SpawnItem("ICD_Flies")
		Stop
	Raise:
		SPOS L 5
		SPOS KJIH 5
		Goto See
	}
}