// Uzi zombie implemented by wildweasel.

ACTOR ICD_UziZombie
{
	Health 40
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "weapons/uzi/fire"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "$OB_ZOMBIE" // "%o was killed by a zombieman."
	DropItem "ICD_UziPickup"
	States
	{
	Spawn:
		POSS A 5 A_Look
		Loop
	See:
		POSS AABBCCDD 3 A_Chase
		Loop
	Missile:
		POSS E 0 A_PlaySound("grunt/sight")
		POSS E 10 A_FaceTarget
		POSS E 1 A_FaceTarget // Unsure why Cory has two of these but I'm replicating it anyway
		POSS F 3 Bright A_CustomBulletAttack(5,5,1,random(3,10),"ICD_BulletPuff",600)
		POSS E 0 A_Jump(184,2)
		POSS E 0 A_CustomMissile("ICD_TRACER1", 32, 0, frandom(-1,1)) //SPARE_ATTACK(PLAYER_TRACER_1),
		POSS E 0 A_CustomMissile("ICD_SSGSmoke1", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))
		POSS E 0 A_CustomMissile("ICD_PistolCasingSpawner", 32, 0, frandom(-35,-25))//SPARE_ATTACK(ENEMY_CASING4),
		POSS F 2 Bright
		POSS E 1 A_CPosRefire
		POSS E 5 A_Jump(253,"See")
	Missile.Reload:
		POSS E 9 A_PlaySound("weapons/generic/emptyclick2")
		POSS E 4 A_PlaySound("weapons/uzi/suzi3")
		POSS E 2 A_PlaySound("weapons/uzi/suzi4")
		POSS E 16	// UZI_CLIP_DROP1
		POSS E 12 A_PlaySound("weapons/uzi/uzi2")
		POSS E 6 A_PlaySound("weapons/uzi/uzi3")
		Goto See
	Pain:
		POSS G 3
		POSS G 3 A_Pain
		POSS G 0 A_Jump(217, "See")
		POSS E 1 A_PlaySound("weapons/grenade/hgren2") //PLAYSOUND(HGREN2),
        POSS E 0 //SPARE_ATTACK(GRENADE_RING_SPAWN),
        POSS E 0 //SPARE_ATTACK(GRENADE_HANDLE_SPAWN),
        POSS E 20 A_CustomMissile("ICD_DroppedGrenade") //SPARE_ATTACK(PLAYER_GREN_DROP),
        POSS E 4 A_ActiveSound //PLAYSOUND(POSACT),
        POSS E 5 A_FaceTarget//FACETARGET,#PAIN:9;*/
		Goto Pain+8
	Death:
		POSS H 5
		POSS I 2 A_Scream
		POSS I 3
		POSS J 5 A_NoBlocking
		POSS K 5
		TNT1 A 0 A_Jump(38,2)
		POSS L -1
		Stop
		POSS L 500
		POSS L 3 A_SpawnItemEx("ICD_Flies")
		POSS L -1 A_SpawnItemEx("ICD_Flies")
		Stop
	XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_NoBlocking
		POSS PQRST 5
		TNT1 A 0 A_Jump(51,2)
		POSS U -1
		Stop
		POSS U 500
		POSS U 3 A_SpawnItemEx("ICD_Flies")
		POSS U -1 A_SpawnItemEx("ICD_Flies")
		Stop
	Raise:
		POSS K 5
		POSS JIH 5
		Goto See
	}
}